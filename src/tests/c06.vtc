varnishtest "Test vmod methods called in wrong vcl methods"

server s1 {
    rxreq
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: recv"
            ],
            "vcl_deliver": [
                "x-deliv: deliv"
            ]
        }
    }
} -start

server s2 {
    rxreq
    expect req.http.x-recv == "recv"
    txresp
} -start

varnish v1 -vcl+backend {
    import headerproxy from "${vmod_topbuild}/src/.libs/libvmod_headerproxy.so";

    sub vcl_recv {
        headerproxy.backend(s1);
        set req.backend_hint = s2;

        headerproxy.call();
        headerproxy.process();
    }

    sub vcl_backend_fetch {
        headerproxy.call();
        headerproxy.process();
        set bereq.http.x-error = headerproxy.error();
        headerproxy.backend(s1);
    }

    sub vcl_backend_response {
        headerproxy.call();
        headerproxy.process();
        set beresp.http.x-error = headerproxy.error();
        headerproxy.backend(s1);
    }

    sub vcl_deliver {
        headerproxy.call();
        headerproxy.process();
        set resp.http.x-error = headerproxy.error();
        headerproxy.backend(s1);
    }

    sub vcl_synth {
        headerproxy.call();
        headerproxy.process();
        set resp.http.x-error = headerproxy.error();
        headerproxy.backend(s1);
    }

    sub vcl_pipe {
        headerproxy.call();
        headerproxy.process();
        set req.http.x-error = headerproxy.error();
        headerproxy.backend(s1);
    }

} -start

client c1 {
    txreq -url "/"
    rxresp
    expect resp.http.x-deliv == "deliv"
    expect resp.http.x-error == ""
} -run

varnish v1 -expect n_object == 1
varnish v1 -expect cache_miss == 1
