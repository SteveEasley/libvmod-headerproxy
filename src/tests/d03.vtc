varnishtest "Test vcl_backend_error"

# Test that backend errors due to retries go through vcl_deliver

server s1 {
    rxreq
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: recv"
            ],
            "vcl_deliver": [
                "x-deliv: deliv"
            ]
        }
    }
} -start

server s2 {
    rxreq
    expect req.http.x-recv == "recv"
    expect req.http.x-error == ""
    txresp
} -start

varnish v1 -vcl+backend {
    import headerproxy from "${vmod_topbuild}/src/.libs/libvmod_headerproxy.so";

    sub vcl_recv {
        headerproxy.backend(s1);
        set req.backend_hint = s2;

        headerproxy.call();
        set req.http.x-error = headerproxy.error();

        return (hash);
    }

    sub vcl_backend_response {
        return (retry);
    }

    sub vcl_backend_error {
        return (deliver);
    }

    sub vcl_deliver {
        headerproxy.process();
        return (deliver);
    }
} -start

varnish v1 -cliok "param.set max_retries 0"

client c1 {
    txreq -url "/"
    rxresp
    expect resp.http.x-deliv == "deliv"
} -run

varnish v1 -expect s_synth == 0
