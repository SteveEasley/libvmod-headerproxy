varnishtest "Test json escaping"

server s1 {
    rxreq
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-single: s'e",
                "x-single-end: s'",
                "x-double: s\"e",
                "x-forward: s\/e",
                "x-back: s\\e",
                "x-tab: s\te",
                "x-newline: s\ne",
                "x-return: s\re"
            ]
        }
    }
} -start

server s2 {
    rxreq
    expect req.http.x-single == {s'e}
    expect req.http.x-single-end == {s'}
    expect req.http.x-double == {s"e}
    expect req.http.x-forward == {s/e}
    expect req.http.x-back == {s\e}
    expect req.http.x-tab == {s\te}
    expect req.http.x-newline == {s\ne}
    expect req.http.x-return == {s\re}
    expect req.http.x-error == ""
    txresp
} -start

varnish v1 -vcl+backend {
    import headerproxy from "${vmod_topbuild}/src/.libs/libvmod_headerproxy.so";

    sub vcl_recv {
        headerproxy.backend(s1);
        set req.backend_hint = s2;

        headerproxy.call();
        set req.http.x-error = headerproxy.error();
    }

    sub vcl_deliver {
        headerproxy.process();
    }
} -start

client c1 {
    txreq -url "/"
    rxresp
} -run
