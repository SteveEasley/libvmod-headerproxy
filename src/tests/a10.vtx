varnishtest "Test restarts"

server s1 {
    rxreq
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: recv1"
            ],
            "vcl_deliver": [
                "x-deliv: deliv1"
            ]
        }
    }

    accept
    rxreq
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: recv2"
            ],
            "vcl_deliver": [
                "x-deliv: deliv2"
            ]
        }
    }

    accept
    rxreq
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: recv3"
            ],
            "vcl_deliver": [
                "x-deliv: deliv3"
            ]
        }
    }
} -start

server s2 {
    rxreq
    expect req.http.x-recv == "recv1"
    expect req.http.x-error == ""
    txresp -status 500

    accept
    rxreq
    expect req.http.x-recv == "recv2"
    expect req.http.x-error == ""
    txresp -status 500

    accept
    rxreq
    expect req.http.x-recv == "recv3"
    expect req.http.x-error == ""
    txresp
} -start


varnish v1 -vcl+backend {
    import headerproxy from "${vmod_topbuild}/src/.libs/libvmod_headerproxy.so";

    sub vcl_recv {
        set req.backend_hint = s2;
        headerproxy.call(s1, "/");
        set req.http.x-error = headerproxy.error();
    }

    sub vcl_deliver {
        headerproxy.process();
        if (resp.status != 200) {
            return (restart);
        }
    }
} -start

client c1 {
    txreq -url "/"
    rxresp
    expect resp.http.x-deliv == "deliv3"
} -run
