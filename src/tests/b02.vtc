varnishtest "Test vlc_recv config change"

server s1  {
    rxreq
    expect req.url == "/0"
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: a"
            ],
        }
    }

    accept
    rxreq
    expect req.url == "/1"
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: b"
            ],
        }
    }
} -start

server s2 {
    rxreq
    expect req.http.x-recv == "a"
    txresp

    rxreq
    expect req.http.x-recv == "b"
    txresp
} -start

varnish v1 -vcl+backend {
    import headerproxy from "${vmod_topbuild}/src/.libs/libvmod_headerproxy.so";

    sub vcl_recv {
        set req.backend_hint = s1;
        set req.backend_hint = s2;

        if (req.url == "/1") {
            headerproxy.url("http://${s1_addr}:${s1_port}/0");
        }
        else if (req.url == "/2") {
            headerproxy.url("http://${s1_addr}:${s1_port}/1");
        }

        headerproxy.call();
    }

    sub vcl_deliver {
        headerproxy.process();
    }
} -start

client c1 {
    txreq -url "/1"
    rxresp
} -run

client c2 {
    txreq -url "/2"
    rxresp
} -run
