varnishtest "Test restarts"

# Test that restarts are not sent to headerproxy

server s1 {
    rxreq
    expect req.url == "/0"
    txresp -hdr "Content-Type: application/json" -body "{}"
} -start

server s2 {
    rxreq
    expect req.http.x-restarts == "1"
    txresp
} -start

varnish v1 -vcl+backend {
    import headerproxy from "${vmod_topbuild}/src/.libs/libvmod_headerproxy.so";

    sub vcl_recv {
        headerproxy.backend(s1);
        set req.backend_hint = s2;

        headerproxy.path(req.restarts + "");

        headerproxy.call();

        set req.http.x-restarts = req.restarts;
    }

    sub vcl_miss {
        if (req.restarts == 0) {
            return (restart);
        }
    }

    sub vcl_deliver {
        set resp.http.x-restarts = req.http.x-restarts;
        headerproxy.process();
    }
} -start

client c1 {
    txreq -url "/"
    rxresp
    expect resp.http.x-restarts == "1"
} -run
