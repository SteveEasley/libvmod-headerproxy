varnishtest "Test round_robin director"

server s1 {
    rxreq
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: s1"
            ]
        }
    }
} -start

server s2 {
    rxreq
    txresp -hdr "Content-Type: application/json" -body {
        {
            "vcl_recv": [
                "x-recv: s2"
            ]
        }
    }
} -start

server s3 {
    rxreq
    expect req.http.x-recv == "s1"
    txresp

    accept
    rxreq
    expect req.http.x-recv == "s2"
    txresp
} -start


varnish v1 -vcl+backend {
    import ${vmod_directors};
    import headerproxy from "${vmod_topbuild}/src/.libs/libvmod_headerproxy.so";

    sub vcl_init {
        new cluster = directors.round_robin();
        cluster.add_backend(s1);
        cluster.add_backend(s2);
    }

    sub vcl_recv {
        set req.backend_hint = s3;

        headerproxy.call(cluster.backend(), "/");
        set req.http.x-error = headerproxy.error();
    }

    sub vcl_deliver {
        headerproxy.process();
    }

} -start

client c1 {
    txreq -url "/"
    rxresp
} -run

client c1 {
    txreq -url "/"
    rxresp
} -run
