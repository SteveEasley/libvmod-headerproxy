varnishtest "Test proxy headers"

# Test proxy sends correct headers
server s1 {
    rxreq
    expect req.method == "GET"
    expect req.http.Via == {HTTP/1.1 VMOD-HeaderProxy}
    expect req.http.X-Forwarded-Method == "POST"
    expect req.http.X-Forwarded-Url == {/foo}
    expect req.http.X-Forwarded-Host == "foo.com"
    txresp -hdr "Content-Type: application/json" -body "{}"
} -start

server s2 {
    rxreq
    expect req.method == "POST"
    expect req.http.Host == "foo.com"
    expect req.http.Via == "foo"
    expect req.http.x-error == ""
    txresp
} -start

varnish v1 -vcl+backend {
    import headerproxy from "${vmod_topbuild}/src/.libs/libvmod_headerproxy.so";

    sub vcl_init {
        headerproxy.url("http://${s1_addr}:${s1_port}");
    }

    sub vcl_recv {
        set req.backend_hint = s1;
        set req.backend_hint = s2;

        headerproxy.call();
        set req.http.x-error = headerproxy.error();
    }

    sub vcl_deliver {
        headerproxy.process();
    }
} -start

client c1 {
    txreq -url "/foo" -req POST -hdr "Host: foo.com" -hdr "Via: foo"
    rxresp
} -run
